#+AUTHOR: Jesse J. Anderson
#+TITLE: jesse’s emacs config
#+PROPERTY: header-args:emacs-lisp :tangle ./init.el

* Package System Setup

Setting up package sources for adding new packages.

#+begin_src emacs-lisp

  ;; Initialize package sources
  (require 'package)
  (setq package-archives '(("melpa" . "https://melpa.org/packages/")
                           ("org" . "https://orgmode.org/elpa/")
                           ("elpa" . "https://elpa.gnu.org/packages/")))

  (package-initialize)
  (unless package-archive-contents
    (package-refresh-contents))

  ;; Initialize use-package on non-Linux platforms
  (unless (package-installed-p 'use-package)
    (package-install 'use-package))

  (require 'use-package)
  (setq use-package-always-ensure t)


  ;; Add line numbers
  (column-number-mode)
  (global-display-line-numbers-mode t)

#+end_src

** Updating Packages

When you want to update all packages manually:

1. `M-x list-packages`
2. `U` (to mark all upgradeable packages for upgrade)
3. `x` (execute the upgrade)

Always restart Emacs after upgrading packages.

*** Automatically Update Packages

Orrrr, let's just do it automatically.

Will I regret this later? Watch this space.

#+begin_src emacs-lisp

  (use-package auto-package-update
    :custom
    (auto-package-update-interval 7)
    (auto-package-update-prompt-before-update t)
    (auto-package-update-hide-results t)
    :config
    (auto-package-update-maybe)
    (auto-package-update-at-time "09:00"))

#+end_src

* Keep Folders Clean

Clean up the configuration Emacs folder.

#+begin_src emacs-lisp

  (use-package no-littering)

  (setq auto-save-file-name-transforms
        `((".*" ,(no-littering-expand-var-file-name "auto-save/") t)))

#+end_src

* Startup Performance

Most important factor to decrease initial load time: load less packages!

Methods of deferring package loads:

- `:hook` - package loads when hook is invoked
- `:bind` - package loads when key binding is used
- `:commands` - package loads when command is used
- `:mode` - package loads when file with specific extension is loaded
- `:after` - package loads when other specific package is loaded
- `:defer` - package loads after startup

To force a package to load at startup

• `:demand t` - ignores other settings and loads at startup

#+begin_src emacs-lisp

  ;; Initial before any optimization: 2.25 seconds, 53 gc

  (defun jj/display-startup-time ()
    (message "Emacs loaded in %s with %d garbage collections."
             (format "%.2f seconds"
                     (float-time
                      (time-subtract after-init-time before-init-time)))
             gcs-done))

  (add-hook 'emacs-startup-hook #'jj/display-startup-time)

#+end_src

* Basic UI Configuration

** General UI

Removes all the extra cruft that comes by default with Emacs. Gets us to a minimal state.

#+begin_src emacs-lisp

  (defvar jj/default-font-size 140)

  ;; Remove ugly startup message
  (setq inhibit-startup-message t)

  (scroll-bar-mode -1)        ; Disable visible scrollbar
  (tool-bar-mode -1)          ; Disable the toolbar
  (tooltip-mode -1)           ; Disable tooltips
  (set-fringe-mode 10)        ; Give some breathing room

  (menu-bar-mode -1)          ; Disabled the menu bar

  ;; Set up the visible bell
  (setq visible-bell t)

  ;; Disable line numbers for some modes
  (dolist (mode '(org-mode-hook
                  term-mode-hook
                  shell-mode-hook
                  treemacs-mode-hook
                  eshell-mode-hook))
    (add-hook mode (lambda () (display-line-numbers-mode 0))))

#+end_src

** Font Configuration

#+begin_src emacs-lisp

  (set-face-attribute 'default nil :font "MonoLisa" :height jj/default-font-size)

  ;; Set the fixed pitch face
  (set-face-attribute 'fixed-pitch nil :font "MonoLisa")

  ;; Set the variable pitch face
  (set-face-attribute 'variable-pitch nil :font "SF Pro Display")


  (custom-set-faces
   ;; custom-set-faces was added by Custom.
   ;; If you edit it by hand, you could mess it up, so be careful.
   ;; Your init file should contain only one such instance.
   ;; If there is more than one, they won't work right.
   '(line-number ((t (:inherit (shadow default) :height 1 :italic nil))))
   '(line-number-current-line ((t (:inherit line-number :italic nil)))))

#+end_src

** Modeline

#+begin_src emacs-lisp

  ;; Make sure to run this on a new machine to get the fonts:
  ;; M-x all-the-icons-install-fonts
  (use-package all-the-icons)

  (use-package doom-modeline
    :init (doom-modeline-mode 1)
    :custom ((doom-modeline-height 36)))

#+end_src

** Theming

#+begin_src emacs-lisp

  (load "~/.emacs.d/themes/eighty-four-theme.el")

  ;; Switch themes with M-x counsel-load-theme
  (use-package doom-themes
    :config
    ;; (load-theme 'doom-dracula t)
    ;; (load-theme 'smyx t)
    (load-theme 'eighty-four t)
    (doom-themes-visual-bell-config) (doom-themes-org-config))

#+end_src

* Keybinding Configuration

** Leader Key Menu

*** Setup

#+begin_src emacs-lisp

  (use-package which-key
    :init (which-key-mode)
    :diminish which-key-mode
    ;; :config
    ;; (setq which-key-idle-delay 0)
    )

  ;; Set up Space leader key
  (use-package general
    :config
    ;; (general-evil-setup t)
    (general-create-definer jj/leader-keys
      :keymaps '(normal insert visual emacs)
      :prefix "SPC"
      :global-prefix "C-SPC"))

  (jj/leader-keys
    "SPC"  '(counsel-M-x :which-key "M-x")
    )

#+end_src

*** Buffers
#+begin_src emacs-lisp

  (jj/leader-keys
    "b"  '(:ignore t :which-key "buffer")
    "bb" '(buffer-menu :which-key "buffer menu")
    "bc" '(kill-this-buffer :which-key "kill active buffer")
    "bm" '((lambda () (interactive) (switch-to-buffer "*Messages*")) :which-key "messages")
    )

#+end_src

*** File

#+begin_src emacs-lisp

  (jj/leader-keys
    "f"  '(:ignore t :which-key "file")
    "fe"  '(:ignore t :which-key "editor")
    "fed" '((lambda () (interactive) (find-file "~/.emacs.d/README.org")) :which-key "emacs config")
    "ff" '(counsel-find-file :which-key "find file")
    "fj" '(dired-jump :which-key "jump to file")
    "fr" '(rename-file :which-key "rename file")
    "fs" '(save-buffer :which-key "save active buffer")
    )

#+end_src

*** Git (Version Control)

#+begin_src emacs-lisp

  (jj/leader-keys
    "g" '(magit-status :which-key "magit"))

#+end_src

*** Org-mode

#+begin_src emacs-lisp

  (jj/leader-keys
    "o"  '(:ignore t :which-key "org-mode")
    "oa" '(org-agenda :which-key "agenda")
    "oc" '(org-capture :which-key "capture")
    "od" '(org-todo :which-key "toggle todo/done")
    "on" '((lambda () (interactive) (org-capture nil "n")) :which-key "add now")
    "ot" '((lambda () (interactive) (org-capture nil "t")) :which-key "add todo")
    )

#+end_src

*** Projects

#+begin_src emacs-lisp

  (jj/leader-keys
    "p" '(projectile-command-map :which-key "projectile"))

#+end_src

*** Toggles

#+begin_src emacs-lisp

  (jj/leader-keys
    "t"  '(:ignore t :which-key "toggles")
    "tt" '(counsel-load-theme :which-key "choose theme")
    "ts" '(hydra-text-scale/body :which-key "scale text")
    )

#+end_src

*** Windows

#+begin_src emacs-lisp

  (jj/leader-keys
    "w"  '(:ignore t :which-key "window")
    "w/" '(split-window-right :which-key "split vertical")
    "wc" '(delete-window :which-key "close window")
    "wh" '(evil-window-left :which-key "select left")
    "wj" '(evil-window-down :which-key "select down")
    "wk" '(evil-window-up :which-key "select up")
    "wl" '(evil-window-right :which-key "select right")
    )

#+end_src

** Vim (Evil) Setup

Will need to test this in isolation to figure out why I have to have `(require 'evil)` to avoid crashing, when docs all suggest the `(use-package evil` should be enough. Might be related to `:ensure t`, but also having issues with other evil packages when I do anything different (e.g., evil-collection, undo-tree).

#+begin_src emacs-lisp

  ;; crashes if I don't have these?
  (setq evil-want-keybinding nil)
  (setq evil-want-C-u-scroll t)
  (require 'evil)

  (use-package evil
    :init
    (setq evil-want-integration t)
    ;; (setq evil-want-keybinding nil)
    ;; (setq evil-want-C-u-scroll t)
    (setq evil-want-C-d-scroll t)
    (setq evil-want-C-i-jump nil)
    :hook (evil-mode . jj/evil-hook)
    :config
    (evil-mode 1)
    (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)
    (define-key evil-insert-state-map (kbd "C-h") 'evil-delete-backward-char-and-join)

    ;; Use visual line motions even outside of visual-line-mode buffers
    (evil-global-set-key 'motion "j" 'evil-next-visual-line)
    (evil-global-set-key 'motion "k" 'evil-previous-visual-line)

    (evil-set-initial-state 'messages-buffer-mode 'normal)
    (evil-set-initial-state 'dashboard-mode 'normal))

  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))

#+end_src

*** Custom Vim Helpers

#+begin_src emacs-lisp

  ;; Add Vim-style redo shortcut: Ctrl-r
  (evil-set-undo-system 'undo-tree)
  (require 'undo-tree)
  (setq evil-undo-system 'undo-tree)
  (global-undo-tree-mode t)
  (add-hook 'evil-local-mode-hook 'turn-on-undo-tree-mode)

#+end_src

** Misc

#+begin_src emacs-lisp

  ;; Make ESC quit prompts
  (global-set-key (kbd "<escape>") 'keyboard-escape-quit)

#+end_src

* Post-Keybinding UI

** Search

#+begin_src emacs-lisp

  (use-package ivy
    :diminish                      ;keeps ivy out of the mode line
    :bind (("C-s" . swiper)        ;inline search similar to vim `/`
           :map ivy-minibuffer-map
           ;; ("TAB" . ivy-alt-done)
           ("C-l" . ivy-alt-done)
           ("C-j" . ivy-next-line)
           ("C-k" . ivy-previous-line)
           :map ivy-switch-buffer-map
           ("C-k" . ivy-previous-line)
           ("C-l" . ivy-done)
           ("C-d" . ivy-switch-buffer-kill)
           :map ivy-reverse-i-search-map
           ("C-k" . ivy-previous-line)
           ("C-d" . ivy-reverse-i-search-kill))
    :config
    (ivy-mode 1))

  (use-package rainbow-delimiters
    :hook (prog-mode . rainbow-delimiters-mode))

  (use-package ivy-rich
    :init
    (ivy-rich-mode 1))

  ;; Improved functions search
  (use-package counsel
    :bind (("M-x" . counsel-M-x)
           ("C-x b" . counsel-ibuffer)
           ("C-x C-f" . counsel-find-file)
           :map minibuffer-local-map
           ("C-r" . counsel-minibuffer-history)))

#+end_src

*** Helpful - Improved Help Commands

#+begin_src emacs-lisp

  ;; Improved helpers
  (use-package helpful
    ;; :commands (helpful-callable helpful-variable helpful-command helpful-key)
    :custom
    (counsel-describe-function-function #'helpful-callable)
    (counsel-describe-variable-function #'helpful-variable)
    :bind
    ([remap describe-function] . counsel-describe-function)
    ([remap describe-command] . helpful-command)
    ([remap describe-variable] . counsel-describe-variable)
    ([remap describe-key] . helpful-key))

#+end_src

** Text Scaling

#+begin_src emacs-lisp

  (use-package hydra)

  (defhydra hydra-text-scale (:timeout 4)
    "scale text"
    ("j" text-scale-increase "in")
    ("k" text-scale-decrease "out")
    ("f" nil "finished" :exit t))

#+end_src

* Structure Templates

#+begin_src emacs-lisp

  (require 'org-tempo)

  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
  (add-to-list 'org-structure-template-alist '("ex" . "src elixir"))
  (add-to-list 'org-structure-template-alist '("js" . "src javascript"))

#+end_src

* Org Mode

** Basic Config

#+begin_src emacs-lisp

  (defun jj/org-mode-setup ()
    (org-indent-mode)
    ;; (variable-pitch-mode 1)
    ;; (auto-fill-mode 0)
    ;; (setq evil-auto-indent nil)
    (visual-line-mode 1))

  (use-package org
    :hook (org-mode . jj/org-mode-setup)
    :config
    (setq org-ellipsis " ▼")
    ;; (setq org-hide-emphasis-markers nil)
    (setq org-agenda-start-with-log-mode t)
    (setq org-log-done 'time)
    (setq org-log-into-drawer t)
    (setq org-agenda-files
          '("~/.emacs.d/org/now.org"))
    )

  (use-package org-bullets
    :hook (org-mode . org-bullets-mode)
    :custom
    (org-bullets-bullet-list '("◉" "○" "●" "○" "●" "○" "●")))

  ;;  Replace list hyphen with dot
  (font-lock-add-keywords 'org-mode
                          '(("^ *\\([-]\\) "
                             (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))

  (require 'org-indent)

  (defun jj/org-mode-visual-fill ()
    (setq visual-fill-column-width 100
          visual-fill-column-center-text t)
    (visual-fill-column-mode 1))

  ;; Wraps long lines
  (use-package visual-fill-column
    :hook (org-mode . jj/org-mode-visual-fill))

  (setq
   org-capture-templates
   '(
     ("n" "What I'm working on now" entry (file+olp+datetree "~/.emacs.d/org/now.org")
      "* %T %?\n%l\n%i" :tree-type week)
     ("t" "Create a TODO for today" entry (file+olp+datetree "~/.emacs.d/org/now.org")
      "* TODO %?" :tree-type week)))

#+end_src

** Configure Babel Languages

#+begin_src emacs-lisp

  (org-babel-do-load-languages
   'org-babel-load-languages
   '((emacs-lisp . t)
     (python . t)))

  (setq org-confirm-babel-evaluate nil)

#+end_src

** Auto-tangle Configuration Files

#+begin_src emacs-lisp

  ;; Automatically tangle our emacs.org config file when we save it
  (defun jj/org-babel-tangle-config ()
    (when (string-equal (buffer-file-name)
                        (expand-file-name "~/.emacs.d/README.org"))
      ;; Dynamic scoping to the rescue
      (let ((org-confirm-babel-evaluate nil))
        (org-babel-tangle))))

  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'jj/org-babel-tangle-config)))

#+end_src

* Development

** Commenting

#+begin_src emacs-lisp

  (use-package evil-nerd-commenter
    :bind ("M-/" . evilnc-comment-or-uncomment-lines)
    :bind ("M-;" . evilnc-comment-or-uncomment-lines)
    )

#+end_src

** Languages

*** Elixir

#+begin_src emacs-lisp

  (defun jj/elixir-format-buffer ()
    (interactive)
    (lsp-format-buffer))

  (use-package elixir-mode
    :init
    ;; (add-to-list 'auto-mode-alist '("\\.heex\\'" . elixir-mode))
    :hook (elixir-mode . (lambda () (add-hook 'before-save-hook
                                              'jj/elixir-format-buffer
                                              nil
                                              t))))

  (defun jj/lsp-mode-setup-completion ()
    (setf (alist-get 'styles (alist-get 'lsp-capf completion-category-defaults))
          '(orderless)))

  (add-hook 'lsp-mode-hook 'jj/lsp-mode-setup-completion)

#+end_src

**** Elixir Theme Hack for custom Eighty-Four Theme

***** Brackets

#+begin_src emacs-lisp

  (defface elixir-bracket-face
    '((t (:foreground "#fcfcfc")))
    "Face for < and > syntax.")

  ;; Conditionally add bracket colors when using eighty-four theme
  (defun elixir-add-bracket-face ()
    (when (member 'eighty-four custom-enabled-themes)
      (font-lock-add-keywords
       nil ;; current buffer
       '(
         ("\\(<%=\\)[^%]*\\(%>\\)" (1 'elixir-bracket-face) (2 'elixir-bracket-face))
         ("\\(<%\\)[^%]*\\(%>\\)" (1 'elixir-bracket-face) (2 'elixir-bracket-face))
         ("\\(<\\.\\)[^>]*\\(/?>\\)" (1 'elixir-bracket-face) (2 'elixir-bracket-face))
         ("\\(</\\.\\)[^>]*\\(>\\)" (1 'elixir-bracket-face) (2 'elixir-bracket-face))
         ("\\(</\\)[^>]*\\(>\\)" (1 'elixir-bracket-face) (2 'elixir-bracket-face))
         ("\\(<\\)[^>]*\\(/?>\\)" (1 'elixir-bracket-face) (2 'elixir-bracket-face))
         ("\\(</\\)[^>]*\\(>\\)" (1 'elixir-bracket-face) (2 'elixir-bracket-face))
         )
       t ;; append
       )))

  (add-hook 'elixir-mode-hook 'elixir-add-bracket-face)
  (add-hook 'load-theme-after-hook 'elixir-add-bracket-face)

#+end_src

***** Heex Major Mode

#+begin_src emacs-lisp

  (define-derived-mode heex-mode elixir-mode "Heex"
    "Major mode for Elixir's Heex templates."
    (setq font-lock-defaults '((font-lock-keywords) nil nil))

    (font-lock-add-keywords
     nil ;; current buffer
     '(
       ("\\(<[^>]+>\\)\\([^<]*\\)\\(<\\/[^>]+>\\)" (2 'elixir-inner-text-face))
       )
     t ;; append
     ))

  ;; Associate .heex files with heex-mode
  (add-to-list 'auto-mode-alist '("\\.heex\\'" . heex-mode))

#+end_src

****** Custom Face Definitions for Heex Mode

#+begin_src emacs-lisp

  ;; init.el
  (defface elixir-equal-face nil "")
  (defface elixir-keyword-face nil "")
  (defface elixir-attr-face nil "")
  (defface elixir-number-face nil "")
  (defface elixir-at-variable-face nil "")

  (defun heex-add-custom-faces ()
    (when (member 'eighty-four custom-enabled-themes)
      (font-lock-add-keywords
       nil ;; current buffer
       '(
         ("\\(!=\\|=\\)" (1 'elixir-equal-face))
         ("\\(<%[^>]*\\)\\(if\\|do\\|else\\|end\\)[^>]*\\(%>\\)" (2 'elixir-keyword-face))
         ;; ("\\(<[^>]+\\)\\(:\\w+=\\)" (2 'elixir-attr-face))
         ;; ("\\b\\w+\\s*=\\s*\\{?@?:?\\w+\\}?" (0 'elixir-attr-face))
         ("\\b\\([0-9]+\\)\\b" (1 'elixir-number-face))
         ;; ("\\(<%=\\|<%\\)\\([^>]*@\\w+[^>]\\)\\(%>\\)" (2 'elixir-at-variable-face))
         )
       t ;; append
       )))

  (add-hook 'heex-mode-hook 'heex-add-custom-faces)
  (add-hook 'load-theme-after-hook 'heex-add-custom-faces)

#+end_src

#+RESULTS:
| elixir-add-inner-text-face | heex-add-custom-faces | elixir-add-bracket-face |

#+begin_src emacs-lisp

  (add-hook 'elixir-mode-hook
            (lambda ()
              (setq font-lock-defaults '((elixir-font-lock-keywords) nil nil))))

  (defface elixir-inner-text-face
    '((t (:foreground "#fcfcfc")))
    "Face for the text inside < and > syntax.")

  (defun elixir-add-inner-text-face ()
    (when (member 'eighty-four custom-enabled-themes)
      (font-lock-add-keywords
       nil ;; current buffer
       '(
         ("\\(<[^>]+>\\)\\(\\w+\\)\\(<\\/[^>]+>\\)" (2 'elixir-inner-text-face))
         )
       t ;; prepend
       )))

  (add-hook 'elixir-mode-hook 'elixir-add-inner-text-face)
  (add-hook 'load-theme-after-hook 'elixir-add-inner-text-face)

#+end_src

#+begin_src emacs-lisp

  ;; (defun elixir-add-capital-letter-face ()
  ;;   (when (member 'eighty-four custom-enabled-themes)
  ;;     (font-lock-add-keywords
  ;;      nil ;; current buffer
  ;;      '(
  ;;        ("\\(<[^>]+>\\)\\([A-Z][^<]*\\)\\(<\\/[^>]+>\\)" (2 'elixir-inner-text-face))
  ;;        )
  ;;      t ;; append
  ;;      )))

  ;; (add-hook 'elixir-mode-hook 'elixir-add-capital-letter-face)
  ;; (add-hook 'load-theme-after-hook 'elixir-add-capital-letter-face)

#+end_src

*** Typescript

#+begin_src emacs-lisp

  (use-package typescript-mode
    :mode "\\.ts\\'"
    ;; :hook (typescript-mode . lsp-deferred)
    :config
    (setq typescript-indent-level 2))

#+end_src

** Language Servers

#+begin_src emacs-lisp

  (defun jj/lsp-mode-setup ()
    (setq lsp-headerline-breadcrumb-segments '(path-up-to-project file symbols))
    (lsp-headerline-breadcrumb-mode))

  (use-package lsp-mode
    :commands (lsp lsp-deferred)
    :hook (
           (elixir-mode . lsp-deferred)
           (elixir-ts-mode . lsp)
           (heex-ts-mode . lsp)
           (js-mode . lsp-deferred)
           (lsp-mode . jj/lsp-mode-setup)
           (typescript-mode . lsp-deferred)
           )
    :init
    (setq lsp-keymap-prefix "C-c l")
    :config
    (lsp-enable-which-key-integration t))

  (use-package lsp-ui
    :hook (lsp-mode . lsp-ui-mode)
    :custom
    (setq lsp-ui-doc-position 'bottom))

  (use-package lsp-treemacs
    :after lsp)

  ;; Search for a symbol within your project
  (use-package lsp-ivy)

  ;; Extra hack to work with my custom heex mode
  (add-to-list 'lsp-language-id-configuration '(heex-mode . "elixir"))

#+end_src

** Company Mode

#+begin_src emacs-lisp

  (use-package company
    :after lsp-mode
    :hook (prog-mode . company-mode)
    :bind
    (:map company-active-map
          ("<tab>" . company-complete-selection))
    (:map lsp-mode-map
          ("<tab>" . company-indent-or-complete-common))
    :custom
    (company-minimum-prefix-length 1)
    (company-idle-delay 0.0))

  ;; Makes the autocomplete menu look a little nicer
  (use-package company-box
    :hook (company-mode . company-box-mode))

#+end_src

** Magit

Magic version control with git.

#+begin_src emacs-lisp

  (use-package magit
    :custom
    (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))

  ;; Github Issues/PRs/Etc in Magit
  ;; NOTE: Currently has an issue with sqlite, so disabling for now
  ;; (use-package forge)

#+end_src

** Projectile

Projectile is a project management library.

#+begin_src emacs-lisp

  (use-package projectile
    :diminish projectile-mode
    :config (projectile-mode)
    :custom ((projectile-completion-system 'ivy))
    :bind-keymap
    ("C-c p" . projectile-command-map)
    :init
    (when (file-directory-p "~/Code")
      (setq projectile-project-search-path '("~/Code")))
    (setq projectile-switch-project-action #'projectile-dired))

  ;; redefine ESC key in projectile-command-map to just close the map
  ;; (define-key projectile-command-map (kbd "<escape>") 'keyboard-escape-quit)

  (use-package counsel-projectile
    :config (counsel-projectile-mode))

#+end_src

** Term

#+begin_src emacs-lisp

  (use-package term
    :config
    (setq explicit-shell-file-name "zsh")
    ;; (setq explicit-zsh-args '())
    (setq term-prompt-regexp "^[^#$%>\n]*[#$%>] *")
    )

  ;; Disabling this because it mutes my colors for some reason
  ;; (use-package eterm-256color
  ;;   :hook (term-mode . eterm-256color-mode))

  ;; set term background to match colors in eighty-four theme
  ;; (defun set-term-background ()
  ;;   (when (eq major-mode 'term-mode)
  ;;     (face-remap-add-relative 'default :foreground "#173347")))

  ;; (add-hook 'buffer-list-update-hook 'set-term-background)

  ;; set term foreground to match colors in eighty-four theme
  (defun set-vterm-foreground ()
    (when (eq major-mode 'vterm-mode)
      (face-remap-add-relative 'default :foreground "#f1f1f1")))

  (add-hook 'buffer-list-update-hook 'set-vterm-foreground)

#+end_src

** Vterm

#+begin_src emacs-lisp

  (use-package vterm
    :commands vterm
    :config
    (setq term-prompt-regexp "^[^#$%>\n]*[#$%>] *")
    (setq vterm-shell "zsh")
    (setq vterm-max-scrollback 10000))

#+end_src

* File Management

** Dired

#+begin_src emacs-lisp

  (setq insert-directory-program "gls" dired-use-ls-dired t)
  (setq dired-listing-switches "-al --group-directories-first")

  (use-package dired
    :ensure nil
    :commands (dired dired-jump)
    :bind (("C-x C-j" . dired-jump))
    :config
    (evil-collection-define-key 'normal 'dired-mode-map
      "h" 'dired-single-up-directory
      "l" 'dired-single-buffer)
    )

  ;; Use single dired buffer
  (use-package dired-single)

  (use-package all-the-icons-dired
    :hook (dired-mode . all-the-icons-dired-mode))

  (require 'dired-aux)

  ;; Toggle hiding dotfiles
  (use-package dired-hide-dotfiles
    :hook (dired-mode . dired-hide-dotfiles-mode)
    :config
    (evil-collection-define-key 'normal 'dired-mode-map
      "H" 'dired-hide-dotfiles-mode))

#+end_src

*** Dired Open

Define which external program will open certain extensions. Not using it right now, but leaving the code here for when I do want to do this later for certain file types, likely images and pdfs.

#+begin_src emacs-lisp

  ;; (use-package dired-open
  ;;   :config
  ;;   (setq dired-open-extensions `(
  ;;                                 ;; {ext} . {app}
  ;;                                 ;; ("png" . "feh")
  ;;                                 ;; ("mkv" . "mpv")
  ;;                                 )))

#+end_src

* Random Extras

** Prevent Backtrace Takeover

#+begin_src emacs-lisp

  ;; Prevent Backtrace from taking over the buffer on an error
  (setq debug-on-error nil)

#+end_src

** Auto Indent Org Files on Save

#+begin_src emacs-lisp

  (defun jj/org-indent-source-blocks ()
    "Indent all source blocks in the current org-mode buffer."
    (when (eq major-mode 'org-mode)
      (org-element-map (org-element-parse-buffer) 'src-block
        (lambda (src-block)
          (let ((begin (org-element-property :begin src-block))
                (end (org-element-property :end src-block)))
            (save-excursion
              (goto-char begin)
              (org-indent-block)))))))

  (add-hook 'before-save-hook #'jj/org-indent-source-blocks)

#+end_src

